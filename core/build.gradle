// FIXME(https://docs.gradle.org/4.1-rc-2/userguide/plugins.html#sec:build_scripts_only):
// Replace buildscript+apply with just
// plugins {
//     id 'gradle-node-plugin' version '1.2.0'
// }
buildscript {
    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'com.moowork.gradle:gradle-node-plugin:1.2.0'
    }
}

plugins {
    id 'java-library'
    id 'jacoco'
}
apply from: "$rootDir/quality/quality-java.gradle"
apply plugin: com.moowork.gradle.node.NodePlugin

node {
    download = true
}

task convertBoltToFirebaseRules(type: NodeTask, dependsOn: npmInstall) {
    script = file("${node.nodeModulesDir}/node_modules/.bin/firebase-bolt")
    args = [
        "${rootProject.projectDir}/firebase/rules.bolt",
        '-o', "${project.buildDir}/delern-rules.json",
    ]
}

testClasses.dependsOn convertBoltToFirebaseRules
resolveDependencies.dependsOn npmInstall

jacoco {
    toolVersion jacocoVersion
}
jacocoTestReport {
    reports {
        xml.enabled true
    }
}

dependencies {
    compile "org.slf4j:slf4j-api:$slf4jVersion"
    compile "io.reactivex.rxjava2:rxjava:$rxJavaVersion"

    // Markdown
    compile "com.atlassian.commonmark:commonmark:$commonmarkVersion"
    compile "com.atlassian.commonmark:commonmark-ext-gfm-tables:$commonmarkVersion"

    compile 'com.google.firebase:firebase-admin:5.2.0', {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
    }

    testCompile 'junit:junit:4.12'
    testCompile 'io.jsonwebtoken:jjwt:0.9.0'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile "uk.org.lidalia:slf4j-test:1.2.0"
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"
