apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'

ext {
    git = org.ajoberstar.grgit.Grgit.open()
    gitVersionCode = git.tag.list().size()
    gitVersionName = "${git.describe()}${git.status().clean ? "" : "*"}"
    firebaseVersion = "10.0.1"
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.3"
    signingConfigs {
        release {
            storeFile file("delern.jks")
            keyAlias "delern"
            storePassword System.getenv("ANDROID_KEYSTORE_PASSWORD")
            keyPassword System.getenv("ANDROID_KEYSTORE_KEY_PASSWORD")
        }
    }
    defaultConfig {
        applicationId "org.dasfoo.delern"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode gitVersionCode
        versionName gitVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            // getDefaultProguardFile searches for the files in path-to-android-sdk/tools/proguard/.
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            play {
                track = 'alpha'
                jsonFile = file('play-store-service-account.json')
                uploadImages = true
            }
        }
        debug {
            // https://developers.google.com/android/guides/google-services-plugin
            //applicationIdSuffix '.debug'
            // To apply Proguard (e.g. to test the configuration), simply copy over
            // minifyEnabled and proguardFiles from "release" build type and add:
            //   debuggable false
        }
    }
    lintOptions {
        // https://google.github.io/android-gradle-dsl/current/com.android.build.gradle.internal.dsl.LintOptions.html
        // Enable all warnings, even those disabled by default.
        checkAllWarnings true
        // We use ProGuard to remove specific log levels.
        disable 'LogConditional'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:design:24.2.1'
    compile 'com.android.support:recyclerview-v7:24.2.1'
    compile 'com.yqritc:recyclerview-flexibledivider:1.4.0'
    compile 'com.android.support:cardview-v7:24.2.1'
    compile 'com.android.support:support-v4:24.2.1'
    compile "com.google.android.gms:play-services-auth:$firebaseVersion"
    compile "com.google.firebase:firebase-core:$firebaseVersion"
    compile "com.google.firebase:firebase-database:$firebaseVersion"
    compile "com.google.firebase:firebase-auth:$firebaseVersion"
    compile "com.google.firebase:firebase-messaging:$firebaseVersion"
    compile "com.google.android.gms:play-services-appinvite:$firebaseVersion"
    compile "com.google.firebase:firebase-analytics:$firebaseVersion"
    compile "com.google.firebase:firebase-crash:$firebaseVersion"
    compile "com.firebaseui:firebase-ui-database:1.0.0"
    testCompile 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'
