apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-crash'
apply plugin: 'com.github.triplet.play'
apply plugin: 'io.fabric'
apply from: 'config/quality.gradle'

ext {
    git = org.ajoberstar.grgit.Grgit.open()
    // Alternatively: grgit.log(includes:['HEAD']).size()
    gitVersionCode = git.tag.list().size() + 20
    gitVersionName = "${git.describe()}${git.status().clean ? "" : "*"}"
    firebaseVersion = '10.2.6'
    sdkVersion = 25
    leakCanaryVersion = '1.5.1'
    supportVersion = "${sdkVersion}.3.1"
}

android {
    compileSdkVersion sdkVersion
    buildToolsVersion "${sdkVersion}.0.2"
    testBuildType 'instrumented'

    signingConfigs {
        release {
            storeFile file('delern.jks')
            keyAlias 'delern'
            storePassword System.getenv('ANDROID_KEYSTORE_PASSWORD')
            keyPassword System.getenv('ANDROID_KEYSTORE_KEY_PASSWORD')
        }
    }
    defaultConfig {
        applicationId 'org.dasfoo.delern'
        minSdkVersion 15
        targetSdkVersion sdkVersion
        versionCode gitVersionCode
        versionName gitVersionName
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        // The VectorDrawableCompat class in the Support Library allows you
        // to support VectorDrawable in Android 2.1 (API level 7) and higher.
        // https://developer.android.com/studio/write/vector-asset-studio.html
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            // Building a release to run Robo tests and publish to Google Play.
            signingConfig signingConfigs.release
            play {
                track = 'alpha'
                jsonFile = file('play-store-service-account.json')
                uploadImages = true
            }
            buildConfigField 'boolean', 'ENABLE_CRASHLYTICS', 'true'
            buildConfigField 'boolean', 'ENABLE_ANONYMOUS_SIGNIN', 'false'
            manifestPlaceholders = [ appName: 'Delern' ]

            minifyEnabled true
            // getDefaultProguardFile searches for the files in path-to-android-sdk/tools/proguard/.
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-release.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-test.pro'
        }
        instrumented {
            // Instrumented tests that require release-style optimizations.
            initWith release
            signingConfig signingConfigs.debug
            // Use Anonymous auth to skip the login intent.
            buildConfigField 'boolean', 'ENABLE_ANONYMOUS_SIGNIN', 'true'
            applicationIdSuffix '.instrumented'
            manifestPlaceholders = [ appName: 'Delern TEST' ]

            if (project.hasProperty('isolatedRun')) {
                // When running instrumented tests for external Pull Request, none of the real
                // Firebase projects are available. Therefore, we use local Firebase server and
                // disable Crashlytics (which will otherwise fail ProGuard mapping upload).
                buildConfigField 'boolean', 'ENABLE_CRASHLYTICS', 'false'
                ext.enableCrashlytics = false
                versionNameSuffix '-instrumented-local'
                // Have to set it here to connect to local Firebase, but since resValue cannot
                // override google-services.json, have to use it in all the other places.
                resValue 'string', 'firebase_database_url', 'ws://10.0.2.2:5533'
            } else {
                // Running instrumented tests on a developer workstation on in the Firebase Test
                // Lab. In both cases APK is signed by a key with an access to a real (but debug-only)
                // Firebase project.
                buildConfigField 'boolean', 'ENABLE_CRASHLYTICS', 'true'
                versionNameSuffix '-instrumented-remote'
                // Enabling Crashlytics makes debugging instrumented tests failures easier.
                resValue 'string', 'firebase_database_url', 'https://delern-debug.firebaseio.com'
            }
        }
        debug {
            // https://developers.google.com/android/guides/google-services-plugin
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
            manifestPlaceholders = [ appName: 'Delern DEBUG' ]
            // To apply Proguard (e.g. to test the configuration), simply copy over
            // minifyEnabled and proguardFiles from "release" build type and add:
            //   debuggable false
            ext.enableCrashlytics = false
            buildConfigField 'boolean', 'ENABLE_CRASHLYTICS', 'true'
            buildConfigField 'boolean', 'ENABLE_ANONYMOUS_SIGNIN', 'false'
            resValue 'string', 'firebase_database_url', 'https://delern-debug.firebaseio.com'
        }
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"
    compile "com.android.support:cardview-v7:$supportVersion"
    compile "com.android.support:support-v4:$supportVersion"

    // TODO(dotdoom): update Firebase to 11, fix local run, remove noinspection
    //noinspection GradleDependency
    compile "com.google.android.gms:play-services-auth:$firebaseVersion"
    compile "com.google.firebase:firebase-core:$firebaseVersion"
    compile "com.google.firebase:firebase-database:$firebaseVersion"
    compile "com.google.firebase:firebase-auth:$firebaseVersion"
    compile "com.google.firebase:firebase-messaging:$firebaseVersion"
    //noinspection GradleDependency
    compile "com.google.android.gms:play-services-appinvite:$firebaseVersion"
    compile "com.google.firebase:firebase-analytics:$firebaseVersion"
    compile "com.google.firebase:firebase-config:$firebaseVersion"

    //noinspection NewerVersionAvailable
    compile 'com.firebaseui:firebase-ui-database:1.2.0'
    compile 'com.yqritc:recyclerview-flexibledivider:1.4.0'
    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'com.github.bumptech.glide:glide:3.8.0'

    debugCompile "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    instrumentedCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    testCompile 'junit:junit:4.12'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true;
    }
}

apply plugin: 'com.google.gms.google-services'
