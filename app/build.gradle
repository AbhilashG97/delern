apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-crash'
apply plugin: 'com.github.triplet.play'
apply plugin: 'checkstyle'
apply plugin: 'pmd'

checkstyle {
    // https://docs.gradle.org/current/dsl/org.gradle.api.plugins.quality.CheckstyleExtension.html
    toolVersion '7.3'
}

pmd {
    // https://docs.gradle.org/current/dsl/org.gradle.api.plugins.quality.PmdExtension.html
    toolVersion '5.5.2'
}

// Have to create our own tasks because Android's sourceSets:
// https://android.googlesource.com/platform/tools/build/+/master/gradle/src/main/groovy/com/android/build/gradle/internal/api/DefaultAndroidSourceSet.java
// are not compatible with Gradle sourceSets:
// https://docs.gradle.org/current/javadoc/org/gradle/api/tasks/SourceSet.html
// therefore checkstyle/pmd/etc constructors doesn't see them.

// TODO(dotdoom): enable checkstyle and pmd for all Gradle projects

task checkstyle(type: Checkstyle) {
    // https://docs.gradle.org/current/dsl/org.gradle.api.plugins.quality.Checkstyle.html
    source android.sourceSets.main.getJavaDirectories()
    classpath = files()
    ignoreFailures = false
}

task pmd(type: Pmd) {
    // https://docs.gradle.org/current/dsl/org.gradle.api.plugins.quality.Pmd.html
    source android.sourceSets.main.getJavaDirectories()
    classpath = files()
    ignoreFailures = false
    ruleSetFiles = files('config/pmd/ruleset.xml')
    consoleOutput = true
}

project.afterEvaluate {
    tasks.withType(tasks['lint'].class) {
        dependsOn 'checkstyle', 'pmd'
    }
}

ext {
    git = org.ajoberstar.grgit.Grgit.open()
    // Alternatively: grgit.log(includes:['HEAD']).size()
    gitVersionCode = git.tag.list().size() + 20
    gitVersionName = "${git.describe()}${git.status().clean ? "" : "*"}"
    firebaseVersion = '10.0.1'
    sdkVersion = 25
    leakCanaryVersion = '1.5'
    supportVersion = "${sdkVersion}.1.0"
}

android {
    compileSdkVersion sdkVersion
    buildToolsVersion "${sdkVersion}.0.1"
    testBuildType 'release'

    signingConfigs {
        release {
            storeFile file('delern.jks')
            keyAlias 'delern'
            storePassword System.getenv('ANDROID_KEYSTORE_PASSWORD')
            keyPassword System.getenv('ANDROID_KEYSTORE_KEY_PASSWORD')
        }
    }
    defaultConfig {
        applicationId 'org.dasfoo.delern'
        minSdkVersion 15
        targetSdkVersion sdkVersion
        versionCode gitVersionCode
        versionName gitVersionName
        testInstrumentationRunner 'org.dasfoo.delern.TestButlerEnabledRunner'
        // The VectorDrawableCompat class in the Support Library allows you
        // to support VectorDrawable in Android 2.1 (API level 7) and higher.
        // https://developer.android.com/studio/write/vector-asset-studio.html
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled true
            // getDefaultProguardFile searches for the files in path-to-android-sdk/tools/proguard/.
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-release.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-test.pro'
            play {
                track = 'alpha'
                jsonFile = file('play-store-service-account.json')
                uploadImages = true
            }
        }
        debug {
            signingConfig signingConfigs.debug
            // https://developers.google.com/android/guides/google-services-plugin
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
            // To apply Proguard (e.g. to test the configuration), simply copy over
            // minifyEnabled and proguardFiles from "release" build type and add:
            //   debuggable false
        }
    }
    // Have to use flavors instead of buildTypes because testBuildType can only
    // be set to a single buildType, which fortunately can have multiple flavors.
    productFlavors {
        normal {
            signingConfig signingConfigs.release
        }
        instrumentedLocal {
            // A flavor for running instrumented tests inside Circle CI, e.g. for external
            // Pull Requests, where a real keystore is not available. APK is therefore signed
            // by a key from a locally-generated debug.keystore, and doesn't have access to a real
            // Firebase server. It will require a local Firebase to be running, the application
            // should use Anonymous auth to skip the login intent.
            signingConfig signingConfigs.debug
            applicationIdSuffix '.instrumented'
            versionNameSuffix '-instrumented-local'
        }
        instrumentedRemote {
            // A flavor for running instrumented tests in the Firebase Test Lab (which is only used
            // on Release phase). APK is signed by a key with an access to a real (but debug-only)
            // Firebase project, the application should use Anonymous auth to skip the login intent.
            signingConfig signingConfigs.debug
            applicationIdSuffix '.instrumented'
            versionNameSuffix '-instrumented-remote'
        }
    }
    variantFilter { variant ->
        if (variant.buildType.name == 'debug') {
            // For "debug" we don't need instrumented flavors, because we want to run instrumented
            // tests with ProGuard.
            variant.ignore = !variant.flavors*.name.contains('normal')
        }
    }
    lintOptions {
        // https://google.github.io/android-gradle-dsl/current/com.android.build.gradle.internal.dsl.LintOptions.html
        // Enable all warnings, even those disabled by default.
        checkAllWarnings true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"
    compile "com.android.support:cardview-v7:$supportVersion"
    compile "com.android.support:support-v4:$supportVersion"

    compile "com.google.android.gms:play-services-auth:$firebaseVersion"
    compile "com.google.firebase:firebase-core:$firebaseVersion"
    compile "com.google.firebase:firebase-database:$firebaseVersion"
    compile "com.google.firebase:firebase-auth:$firebaseVersion"
    compile "com.google.firebase:firebase-messaging:$firebaseVersion"
    compile "com.google.android.gms:play-services-appinvite:$firebaseVersion"
    compile "com.google.firebase:firebase-analytics:$firebaseVersion"
    compile "com.google.firebase:firebase-crash:$firebaseVersion"

    compile 'com.firebaseui:firebase-ui-database:1.0.0'
    compile 'com.yqritc:recyclerview-flexibledivider:1.4.0'
    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'

    debugCompile "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    testCompile 'junit:junit:4.12'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile 'com.linkedin.testbutler:test-butler-library:1.2.0'
}

apply plugin: 'com.google.gms.google-services'
