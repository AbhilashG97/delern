apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-crash'
apply plugin: 'com.github.triplet.play'
apply plugin: 'io.fabric'
apply from: 'config/quality.gradle'

ext {
    git = org.ajoberstar.grgit.Grgit.open()
    // Alternatively: grgit.log(includes:['HEAD']).size()
    gitVersionCode = git.tag.list().size() + 20
    gitVersionName = "${git.describe()}${git.status().clean ? "" : "*"}"
    firebaseVersion = '10.2.6'
    sdkVersion = 25
    leakCanaryVersion = '1.5.1'
    supportVersion = "${sdkVersion}.3.1"
}

android {
    compileSdkVersion sdkVersion
    buildToolsVersion "${sdkVersion}.0.2"
    testBuildType 'instrumented'

    signingConfigs {
        release {
            storeFile file('delern.jks')
            keyAlias 'delern'
            storePassword System.getenv('ANDROID_KEYSTORE_PASSWORD')
            keyPassword System.getenv('ANDROID_KEYSTORE_KEY_PASSWORD')
        }
    }
    defaultConfig {
        applicationId 'org.dasfoo.delern'
        minSdkVersion 15
        targetSdkVersion sdkVersion
        versionCode gitVersionCode
        versionName gitVersionName
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        // The VectorDrawableCompat class in the Support Library allows you
        // to support VectorDrawable in Android 2.1 (API level 7) and higher.
        // https://developer.android.com/studio/write/vector-asset-studio.html
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            // Building a release to run Robo tests and publish to Google Play.
            signingConfig signingConfigs.release
            play {
                track = 'alpha'
                jsonFile = file('play-store-service-account.json')
                uploadImages = true
            }
            manifestPlaceholders = [ appName: 'Delern' ]
            buildConfigField 'boolean', 'ENABLE_CRASHLYTICS', 'true'
            buildConfigField 'boolean', 'ENABLE_ANONYMOUS_SIGNIN', 'false'

            minifyEnabled true
            // getDefaultProguardFile searches for the files in path-to-android-sdk/tools/proguard/.
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-release.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-test.pro'
        }
        instrumented {
            // Instrumented tests that require release-style optimizations.
            initWith release

            if (System.properties['idea.platform.prefix']) {
                // Disable obfuscation (slow, strips logger), make debuggable under Android Studio.
                useProguard false
                minifyEnabled false
                debuggable true

                tasks.each { task ->
                    if (task.group == 'verification') {
                        task.enabled = false
                    }
                }
            }

            signingConfig signingConfigs.debug
            applicationIdSuffix '.instrumented'
            versionNameSuffix '-instrumented'
            manifestPlaceholders = [ appName: 'Delern TEST' ]
            // Use Anonymous auth to skip the login intent.
            buildConfigField 'boolean', 'ENABLE_ANONYMOUS_SIGNIN', 'true'
            resValue 'string', 'firebase_database_url', 'https://delern-debug.firebaseio.com'
        }
        debug {
            // https://developers.google.com/android/guides/google-services-plugin
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
            manifestPlaceholders = [ appName: 'Delern DEBUG' ]
            // To apply Proguard (e.g. to test the configuration), simply copy over
            // minifyEnabled and proguardFiles from "release" build type and add:
            //   debuggable false
            if (project.hasProperty('isolatedRun')) {
                ext.enableCrashlytics = false
                buildConfigField 'boolean', 'ENABLE_CRASHLYTICS', 'false'
                resValue 'string', 'firebase_database_url', 'ws://127.0.0.1:5533'
            } else {
                // Do not upload ProGuard mapping for debug (it's not obfuscated).
                ext.alwaysUpdateBuildId = false
                buildConfigField 'boolean', 'ENABLE_CRASHLYTICS', 'true'
                resValue 'string', 'firebase_database_url', 'https://delern-debug.firebaseio.com'
            }
            buildConfigField 'boolean', 'ENABLE_ANONYMOUS_SIGNIN', 'false'
        }
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"
    compile "com.android.support:cardview-v7:$supportVersion"
    compile "com.android.support:support-v4:$supportVersion"

    // TODO(dotdoom): once emulators pick up new Play Services, update Firebase to 11.0.1 (or later)
    //noinspection GradleDependency
    compile "com.google.android.gms:play-services-auth:$firebaseVersion"
    //noinspection GradleDependency
    compile "com.google.android.gms:play-services-appinvite:$firebaseVersion"
    compile "com.google.firebase:firebase-core:$firebaseVersion"
    compile "com.google.firebase:firebase-database:$firebaseVersion"
    compile "com.google.firebase:firebase-auth:$firebaseVersion"
    compile "com.google.firebase:firebase-messaging:$firebaseVersion"
    compile "com.google.firebase:firebase-config:$firebaseVersion"

    //noinspection NewerVersionAvailable
    compile 'com.firebaseui:firebase-ui-database:1.2.0'
    compile 'com.yqritc:recyclerview-flexibledivider:1.4.0'
    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'com.github.bumptech.glide:glide:3.8.0'

    debugCompile "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    instrumentedCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    testCompile 'junit:junit:4.12'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true;

    // ButterKnife
    compile 'com.jakewharton:butterknife:8.6.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.6.0'
    }
    // Dagger2 depencencies
    compile 'com.google.dagger:dagger:2.11'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.11'
    provided 'javax.annotation:jsr250-api:1.0'
}

apply plugin: 'com.google.gms.google-services'
